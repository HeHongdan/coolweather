apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.hehongdan.coolweather"
        minSdkVersion 15
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        //解决Gradle3.0.0后多渠道打包维度统一
        flavorDimensions "versionCode"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        //2/3Lombok插件添加如下配置 开启annotation processor
        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath true
            }
        }
    }

    /** 签名的配置 */
    signingConfigs {
        //加载签名文件资源
        Properties properties = new Properties()
        InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream()
        properties.load(inputStream)
        //读取文件和字段
        def store_path = properties.getProperty('store.dir')
        def store_file = file(store_path)
        def store_password = properties.getProperty('store.password')
        def key_alias = properties.getProperty('alias.name')
        def key_password = properties.getProperty('alias.pass')

        releaseConfig {
            storeFile store_file//签名文件
            storePassword store_password//证书文件的密码
            keyAlias key_alias//证书密钥别名
            keyPassword key_password//证书改密钥的密码
        }
        debugConfig {
            storeFile store_file
            storePassword store_password
            keyAlias key_alias
            keyPassword key_password
        }
    }

    /** 编译类型配置 */
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debugConfig
        }
    }

    /** 多渠道打包 */
    productFlavors {
        hehongdan {
//            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "meijia"]//（AndroidManifest里的占位符）另外写法也有manifestPlaceholders.put("UMENG_CHANNEL_VALUE",'wandoujia')，稍有区别。
//            applicationId "com.hehongdan.coolweather.hehongdan" //自定义包名
//            versionName "1.1"//自定义版本名称
            buildConfigField "boolean", "LOG_DEBUG", "true"
//            buildConfigField "String", "CHANNEL", "hehongdan"
        }
        tencent {//投放应用宝市场
//            buildConfigField "String", "CHANNEL", "tencent"
        }
        baidu {//投放百度市场
//            buildConfigField "String", "CHANNEL", "baidu"
        }
        _360 {//投放360市场
//            buildConfigField "String", "CHANNEL", "_360"
        }
        xiaomi {//投放小米市场
//            buildConfigField "String", "CHANNEL", "xiaomi"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'org.litepal.android:core:1.6.1'
    implementation 'com.squareup.okhttp3:okhttp:3.4.1'
    implementation 'com.google.code.gson:gson:2.7'

    //1/3Lombok插件（免GetSet）
    implementation 'javax.annotation:javax.annotation-api:1.2'
    implementation 'org.projectlombok:lombok:1.16.6'

    //1/3DoraemonKit滴滴调试工具
    debugImplementation 'com.didichuxing.doraemonkit:doraemonkit:2.0.0'
    releaseImplementation 'com.didichuxing.doraemonkit:doraemonkit-no-op:2.0.0'
    //网络进行监控配置
    //在root下的build.gradle文件里面，添加classpath 'com.hujiang.aspectjx:gradle-android-plugin-aspectjx:2.0.4'
    //在app/build.gradle里面还需要启动这个插件apply plugin: 'android-aspectjx'
    //另外Okhttp的实例化代码OkHttpClient.Builder builder = new OkHttpClient().newBuilder();
}
